plugins {
	id 'fabric-loom' version '1.10-SNAPSHOT' apply false
	id 'maven-publish'
	id 'glue' version '0.0.4'
}

glue {
	targets = ['common', '1_17', '1_19', '1_19_3', '1_19_4']
}

subprojects {
	apply plugin: 'fabric-loom'

	version = project.mod_version
	group = project.maven_group

	base {
		archivesName = project.archives_base_name
	}

	java {
		targetCompatibility = sourceCompatibility = project.java_version as Integer
	}

	repositories {
		mavenCentral()
	}

	dependencies {
		// To change the versions see the gradle.properties file
		minecraft "com.mojang:minecraft:${project.minecraft_version}"
		mappings "net.fabricmc:yarn:${project.yarn_mappings}"
		modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

		// Testing engines
		testImplementation "org.junit.jupiter:junit-jupiter-api:${junit_jupiter_version}"
		testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junit_jupiter_version}"
	}

	processResources {
		def toExpand = [
			version: project.version,
			java_version: project.java_version
		]

		inputs.properties toExpand

		filesMatching(["fabric.mod.json", "*.mixins.json"]) {
			expand toExpand
		}
	}

	tasks.withType(JavaCompile).configureEach {
		it.options.release = project.java_version as Integer
	}

	java {
		// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
		// if it is present.
		// If you remove this line, sources will not be generated.
		withSourcesJar()
	}

	jar {
		from("LICENSE") {
			rename { "${it}_${inputs.properties.archivesName}" }
		}
	}

	test {
		useJUnitPlatform()
	}

	// configure the maven publication
	publishing {
		publications {
//			mavenJava(MavenPublication) {
//				from components.java
//			}
		}

		// Select the repositories you want to publish to
		// To publish to maven local, no extra repositories are necessary. Just use the task `publishToMavenLocal`.
		repositories {
			// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
		}
	}
}
