plugins {
	id 'fabric-loom' version '1.10-SNAPSHOT' apply false
	id 'glue' version '0.0.9'
}

glue {
	targets = [
		'common', '1_16', '1_17', '1_19', 'common1_19_4', '1_19_4', '1_20', '1_20_3', '1_20_5'
	]

	// move mixin configs to custom block, so GluePreLaunch can load them automatically for appropriate Java versions
	transform('fabric.mod.json') {
		println 'moving "mixins" to "custom/glue/mixins"'
		def mixins = it.remove('mixins')
		it.withDefault { [:] } ['custom'].withDefault { [:] } ['glue']['mixins'] = mixins
	}
}

subprojects {
	apply plugin: 'fabric-loom'

	loom {
		mixin {
			defaultRefmapName = "minepkg-companion.${project.name}.refmap.json"
		}
	}

	dependencies {
		// To change the versions see the gradle.properties file
		minecraft "com.mojang:minecraft:${project.minecraft_version}"
		mappings "net.fabricmc:yarn:${project.yarn_mappings}"
		modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	}

	version = project.mod_version
	group = project.maven_group

	base {
		archivesName = project.archives_base_name
	}

	processResources {
		def toExpand = [
			'project': project.name,
			version: project.version,
			java_version: project.java_version
		]

		inputs.properties toExpand

		filesMatching(["fabric.mod.json", "*.mixins.json"]) {
			expand toExpand
		}
	}

	java {
		targetCompatibility = sourceCompatibility = project.java_version as Integer
	}

	tasks.withType(JavaCompile).configureEach {
		it.options.release = project.java_version as Integer
	}
}
