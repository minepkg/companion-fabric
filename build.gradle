plugins {
	id 'fabric-loom' version '1.10-SNAPSHOT' apply false
	id 'glue' version '0.0.6'
}

glue {
	targets = [
		'common', '1_17', '1_19', 'common1_19_4', '1_19_4', '1_20', '1_20_3', '1_20_5'
	]
}

subprojects {
	apply plugin: 'fabric-loom'

	loom {
		mixin {
			defaultRefmapName = "minepkg-companion.${project.name}.refmap.json"
		}
	}

	dependencies {
		// To change the versions see the gradle.properties file
		minecraft "com.mojang:minecraft:${project.minecraft_version}"
		mappings "net.fabricmc:yarn:${project.yarn_mappings}"
		modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	}

	version = project.mod_version
	group = project.maven_group

	base {
		archivesName = project.archives_base_name
	}

	processResources {
		def toExpand = [
			'project': project.name,
			version: project.version,
			min_java_version: project.min_java_version
		]

		inputs.properties toExpand

		filesMatching(["fabric.mod.json", "*.mixins.json"]) {
			expand toExpand
		}
	}

	java {
		targetCompatibility = sourceCompatibility = project.java_version as Integer
	}

	tasks.withType(JavaCompile).configureEach {
		it.options.release = project.java_version as Integer
	}
}
